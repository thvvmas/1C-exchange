
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДеревоФормы = РеквизитФормыВЗначение("ДеревоМетаданных");
	
	СтрокаКласса = ДеревоФормы.Строки.Добавить();
	СтрокаКласса.Имя = "Подсистемы";
	СтрокаКласса.Картинка = БиблиотекаКартинок.Подсистема;
	
	Для Каждого ОбъектМетаданных Из Метаданные.Подсистемы Цикл
		
		СтрокаОбъекта = СтрокаКласса.Строки.Добавить();
		СтрокаОбъекта.Картинка	= СтрокаКласса.Картинка;
		СтрокаОбъекта.Имя		= ОбъектМетаданных.Имя;
		СтрокаОбъекта.Синоним	= ОбъектМетаданных.Синоним;
		СтрокаОбъекта.ПолноеИмя	= ОбъектМетаданных.ПолноеИмя();
		
		ВывестиПодсистемыРекурсивно(ОбъектМетаданных.Подсистемы, СтрокаОбъекта, СтрокаКласса.Картинка);
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоФормы, "ДеревоМетаданных");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбраннаяСтрока = ДеревоМетаданных.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ОповеститьОВыборе(ВыбраннаяСтрока.ПолноеИмя);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиПодсистемыРекурсивно(Подсистемы, РодительскаяСтрока, Картинка)
	
	Для Каждого ПодсистемаЕдиница Из Подсистемы Цикл
		
		СтрокаОбъекта = РодительскаяСтрока.Строки.Добавить();
		СтрокаОбъекта.Картинка	= Картинка;
		СтрокаОбъекта.Имя		= ПодсистемаЕдиница.Имя;
		СтрокаОбъекта.Синоним	= ПодсистемаЕдиница.Синоним;
		СтрокаОбъекта.ПолноеИмя	= ПодсистемаЕдиница.ПолноеИмя();
		
		ВывестиПодсистемыРекурсивно(ПодсистемаЕдиница.Подсистемы, СтрокаОбъекта, Картинка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции